@ECHO OFF
TITLE %CLOUDCOINMANAGERPORTABLE_name% - Checking Version
IF "%~1" == "" EXIT
SET CLOUDCOINMANAGERPORTABLE_version=
SET CLOUDCOINMANAGERPORTABLE_new_version=
CD /D "%~dp0"
CALL :version_done

IF NOT EXIST "version.txt" GOTO version_redo_choice
IF EXIST "version.txt" FOR /F "tokens=* delims=" %%G in (version.txt) DO IF NOT "%%~G" == "" CALL :set_current_version "%%~G" & GOTO skip_current_version
GOTO skip_current_version
:set_current_version
SET CLOUDCOINMANAGERPORTABLE_version=%~1
EXIT /B
:skip_current_version
TITLE %CLOUDCOINMANAGERPORTABLE_name% %CLOUDCOINMANAGERPORTABLE_version%
IF DEFINED CLOUDCOINMANAGERPORTABLE_no_version_check GOTO version_done

IF EXIST "%CLOUDCOINMANAGERPORTABLE_home_dir%\Settings\version.txt" (
    FOR %%G IN ("%CLOUDCOINMANAGERPORTABLE_home_dir%\Settings\version.txt") DO SET CLOUDCOINMANAGERPORTABLE_file_time=%%~tG
    SET CLOUDCOINMANAGERPORTABLE_file_time=%CLOUDCOINMANAGERPORTABLE_file_time:~0,10%
    SET CLOUDCOINMANAGERPORTABLE_time=%DATE%
    SET CLOUDCOINMANAGERPORTABLE_time=%CLOUDCOINMANAGERPORTABLE_time:~-10%
    IF "%CLOUDCOINMANAGERPORTABLE_time%" == "%CLOUDCOINMANAGERPORTABLE_file_time%" GOTO version_done
)

TITLE %CLOUDCOINMANAGERPORTABLE_name% %CLOUDCOINMANAGERPORTABLE_version% - Checking Version
ECHO. & ECHO Checking for update...
FOR /F "tokens=*" %%G IN ('BITSADMIN /LIST 1^>NUL') DO CALL :version_remove_job "%%G"
BITSADMIN /CANCEL "CLOUDCOINMANAGERPORTABLE_UPDATE_BITFC3UEVPCG8BJNGFRU" > NUL 2>&1
PING github.com -n 1 -w 5000 > NUL 2>&1 || GOTO version_done
BITSADMIN /CREATE /DOWNLOAD "CLOUDCOINMANAGERPORTABLE_UPDATE_BITFC3UEVPCG8BJNGFRU" > NUL 2>&1
BITSADMIN /SETMAXDOWNLOADTIME "CLOUDCOINMANAGERPORTABLE_UPDATE_BITFC3UEVPCG8BJNGFRU" 20 > NUL 2>&1
BITSADMIN /SETNOPROGRESSTIMEOUT "CLOUDCOINMANAGERPORTABLE_UPDATE_BITFC3UEVPCG8BJNGFRU" 5 > NUL 2>&1
BITSADMIN /SETMINRETRYDELAY "CLOUDCOINMANAGERPORTABLE_UPDATE_BITFC3UEVPCG8BJNGFRU" 0 > NUL 2>&1
BITSADMIN /SETNOTIFYCMDLINE "CLOUDCOINMANAGERPORTABLE_UPDATE_BITFC3UEVPCG8BJNGFRU" NULL NULL > NUL 2>&1
BITSADMIN /TRANSFER "CLOUDCOINMANAGERPORTABLE_UPDATE_BITFC3UEVPCG8BJNGFRU" /DOWNLOAD /DYNAMIC "https://github.com/BigRedBrent/CloudCoinManagerPortable/raw/main/version.txt" "%CD%\version.tmp" > NUL 2>&1
BITSADMIN /CANCEL "CLOUDCOINMANAGERPORTABLE_UPDATE_BITFC3UEVPCG8BJNGFRU" > NUL 2>&1
FOR /F "tokens=*" %%G IN ('BITSADMIN /LIST 1^>NUL') DO CALL :version_remove_job "%%G"
GOTO version_skip_remove_job
:version_remove_job
SET CLOUDCOINMANAGERPORTABLE_version_guid=%~1
ECHO %~1 | FIND "CLOUDCOINMANAGERPORTABLE_UPDATE_BITFC3UEVPCG8BJNGFRU" > NUL 2>&1 && BITSADMIN /CANCEL %CLOUDCOINMANAGERPORTABLE_version_guid:~0,38% > NUL 2>&1
EXIT /B
:version_skip_remove_job
CLS
IF NOT EXIST "version.tmp" GOTO version_done

FOR /F "tokens=* delims=" %%G in (version.tmp) DO IF NOT "%%~G" == "" CALL :set_version "%%~G" & GOTO skip_version
GOTO skip_version
:set_version
SET CLOUDCOINMANAGERPORTABLE_new_version=%~1
EXIT /B
:skip_version

CALL :version_done

IF "%CLOUDCOINMANAGERPORTABLE_new_version%" == "" GOTO version_done
IF "%CLOUDCOINMANAGERPORTABLE_version%" == "%CLOUDCOINMANAGERPORTABLE_new_version%" (
    IF NOT EXIST "%CLOUDCOINMANAGERPORTABLE_home_dir%\Settings" MKDIR "%CLOUDCOINMANAGERPORTABLE_home_dir%\Settings" > NUL 2>&1 || GOTO version_done
    ECHO %DATE% %TIME% %CLOUDCOINMANAGERPORTABLE_version%> "%CLOUDCOINMANAGERPORTABLE_home_dir%\Settings\version.txt"
    GOTO version_done
)

TITLE %CLOUDCOINMANAGERPORTABLE_name% %CLOUDCOINMANAGERPORTABLE_version%
:version_redo_choice
IF DEFINED CLOUDCOINMANAGERPORTABLE_no_version_check GOTO version_done
CLS
ECHO. & ECHO Update available: %CLOUDCOINMANAGERPORTABLE_name% %CLOUDCOINMANAGERPORTABLE_new_version% & ECHO.
CHOICE /C 1234567890ABCDEFGHIJKLMNOPQRSTUVWXYZ /M "Download and update [Y/N]?" /N
IF %ERRORLEVEL% == 24 GOTO version_done
IF NOT %ERRORLEVEL% == 35 GOTO version_redo_choice
COPY /V /Y update.cmd update.tmp.cmd || GOTO version_done
START "" update.tmp.cmd "1" & EXIT

:version_done
DEL /Q "*.tmp" > NUL 2>&1
DEL /A:H /Q "*.tmp" > NUL 2>&1
EXIT /B
